# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=('libxcursor' 'libxcursor-devel')
pkgver=1.2.0
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxfixes' 'libxrender')
makedepends=('xorg-util-macros' 'libxfixes-devel' 'libxrender-devel')
source=(${url}releases/individual/lib/libXcursor-${pkgver}.tar.bz2)
sha256sums=('3ad3e9f8251094af6fe8cb4afcf63e28df504d46bfa5a5529db74a505d628782')

prepare() {
  cd libXcursor-${pkgver}
  autoreconf -fvi
}

build() {
  cd libXcursor-${pkgver}
  ./configure \
  --prefix=/usr \
  --build=${CHOST}
  make
  make DESTDIR=${srcdir}/dest install
}

package_libxcursor() {
  pkgdesc="X cursor management library"
  groups=('libraries')

  install -Dm755 ${srcdir}/dest/usr/bin/* -t ${pkgdir}/usr/bin/
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/COPYING \
    -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_libxcursor-devel() {
  pkgdesc="libxcursor headers and libraries"
  groups=('development')
  depends=("libxcursor=${pkgver}" 'libxrender-devel' 'libxfixes-devel')

  install -Dm644 ${srcdir}/dest/usr/include/X11/Xcursor/Xcursor.h \
    -t ${pkgdir}/usr/include/X11/Xcursor/
  install -Dm644 ${srcdir}/dest/usr/lib/libXcursor*.a \
    -t ${pkgdir}/usr/lib/
  install -Dm644 ${srcdir}/dest/usr/lib/pkgconfig/xcursor.pc \
    -t ${pkgdir}/usr/lib/pkgconfig/
  install -Dm644 ${srcdir}/dest/usr/share/man/man3/Xcursor*.3 \
    -t ${pkgdir}/usr/share/man/man3/
}
