# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=('libxrender' 'libxrender-devel')
pkgver=0.9.9
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
makedepends=('xorgproto' 'libx11-devel')
source=(${url}releases/individual/lib/libXrender-${pkgver}.tar.bz2)
sha256sums=('fc2fe57980a14092426dffcd1f2d9de0987b9d40adea663bd70d6342c0e9be1a')

prepare() {
  cd libXrender-${pkgver}
  autoreconf -fvi
}

build() {
  cd libXrender-${pkgver}
  ./configure \
  --prefix=/usr \
  --build=${CHOST}
  make
  make DESTDIR=${srcdir}/dest install
}

package_libxrender() {
  pkgdesc="X Rendering Extension client library"
  depends=('libx11')
  groups=('libraries')

  install -Dm755 ${srcdir}/dest/usr/bin/* -t ${pkgdir}/usr/bin/
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/COPYING \
    -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_libxrender-devel() {
  pkgdesc="LibXrender headers and libraries"
  groups=('development')
  depends=("libxrender=${pkgver}" 'libx11-devel')

  install -Dm644 ${srcdir}/dest/usr/include/X11/extensions/*.h \
    -t ${pkgdir}/usr/include/X11/extensions/
  install -Dm644 ${srcdir}/dest/usr/lib/libXrender*.a \
    -t ${pkgdir}/usr/lib/
  install -Dm644 ${srcdir}/dest/usr/lib/pkgconfig/xrender.pc \
    -t ${pkgdir}/usr/lib/pkgconfig/
  install -Dm644 ${srcdir}/dest/usr/share/doc/libXrender/libXrender.txt \
    -t ${pkgdir}/usr/share/doc/libXrender/
}
