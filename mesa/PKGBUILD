# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=mesa
pkgver=19.1.6
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="https://www.mesa3d.org/"
pkgdesc="An open-source implementation of the OpenGL specification"
makedepends=(python3-mako libxml2 libx11-devel libxdamage-devel xorgproto)
source=("https://mesa.freedesktop.org/archive/mesa-${pkgver}.tar.xz"
        "18.1.3-meson-egl_dri2.patch"
        "19.1.2-meson-gallium_egl.patch"
        "0001-Massage-server-fbconfigs-to-make-them-more-likely-to.patch"
        "0003-Turn-off-DPI-virtualization-for-Windows-DRI-Windows.patch"            "LICENSE")
md5sums=('7dbb40b8d10e89bee0a5bfc85350647b'
         'fbc931e90e2f5b1bedc7214f47b805a4'
         'c5575bf591d2d6b2c664017f532c6aba'
         '67290bc84e1bddfeee11b5c9a86e887e'
         'c04388afba137d74911cb1c817bd05e2'
         'a025946ad525d158bd4b7152d8303fd3')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p2 -i ${srcdir}/18.1.3-meson-egl_dri2.patch
  patch -p2 -i ${srcdir}/19.1.2-meson-gallium_egl.patch
  patch -p1 -i ${srcdir}/0001-Massage-server-fbconfigs-to-make-them-more-likely-to.patch
  patch -p1 -i ${srcdir}/0003-Turn-off-DPI-virtualization-for-Windows-DRI-Windows.patch
}

build() {
  meson --prefix=/usr mesa-${pkgver} build \
    -Dplatforms=x11 \
    -Ddri3=false \
    -Dgallium-drivers=swrast \
    -Dshared-glapi=true \
    -Dgles1=false \
    -Dgles2=true \
    -Dopengl=true \
    -Dgbm=false \
    -Dglx=dri \
    -Degl=true \
    -Dglvnd=false \
    -Dasm=false \
    -Dllvm=false \
    -Dtexture-float=true \
    -Dosmesa=gallium \
    -Dosmesa-bits=8

  # Print config
  meson configure build

  ninja -C build
}

package() {
  DESTDIR=${pkgdir} ninja -C build install
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
