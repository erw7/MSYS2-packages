# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=gdk-pixbuf2
pkgver=2.36.9
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="https://wiki.gnome.org/Projects/GdkPixbuf"
pkgdesc="An image loading library"
depends=(glib2 libpng libtiff libjpeg-turbo libx11 jasper shared-mime-info)
makedepends=(gtk-doc libx11-devel)
install=gdk-pixbuf2.install
source=("https://ftp.gnome.org/pub/gnome/sources/gdk-pixbuf/2.36/gdk-pixbuf-${pkgver}.tar.xz"
        "0001-msys-platform-win32.patch"
        "0002-fix-tiff-loader-compilation.patch"
        "gdk-pixbuf-query-loaders.hook")
md5sums=('2f4bd2dd2a7b6c7234b177e4503ffa94'
         '6dfa4b1314ad702d854516b35e312533'
         'a511787ce453484856b1a403dfc1b0c1'
         '804e51bf470b9c8a96f72904b859aabd')

prepare() {
  cd gdk-pixbuf-${pkgver}
  patch -p1 -i ${srcdir}/0001-msys-platform-win32.patch
  patch -p1 -i ${srcdir}/0002-fix-tiff-loader-compilation.patch
  autoreconf -fvi
}

build() {
  cd gdk-pixbuf-${pkgver}
  ./configure \
  --prefix=/usr \
  --build=${CHOST} \
  --with-libjasper \
  --with-x11 \
  --with-included-loader=png
  make
}

package() {
  cd gdk-pixbuf-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/*.hook -t ${pkgdir}/usr/share/libalpm/hooks/
}
