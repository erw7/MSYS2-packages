# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=('libxdamage' 'libxdamage-devel')
pkgver=1.1.5
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxfixes')
makedepends=('xorgproto')
source=(${url}releases/individual/lib/libXdamage-${pkgver}.tar.bz2)
sha256sums=('b734068643cac3b5f3d2c8279dd366b5bf28c7219d9e9d8717e1383995e0ea45')

prepare() {
  cd libXdamage-${pkgver}
  autoreconf -fvi
}

build() {
  cd libXdamage-${pkgver}
  ./configure \
  --prefix=/usr \
  --build=${CHOST}
  make
  make DESTDIR=${srcdir}/dest install
}

package_libxdamage() {
  pkgdesc="X11 damaged region extension library"
  groups=('libraries')

  install -Dm755 ${srcdir}/dest/usr/bin/* -t ${pkgdir}/usr/bin/
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/COPYING \
    -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_libxdamage-devel() {
  pkgdesc="libxdamage headers and libraries"
  groups=('development')
  depends=("libxdamage=${pkgver}" 'libxfixes-devel')

  install -Dm644 ${srcdir}/dest/usr/include/X11/extensions/Xdamage.h \
    -t ${pkgdir}/usr/include/X11/extensions/
  install -Dm644 ${srcdir}/dest/usr/lib/libXdamage*.a \
    -t ${pkgdir}/usr/lib/
  install -Dm644 ${srcdir}/dest/usr/lib/pkgconfig/xdamage.pc \
    -t ${pkgdir}/usr/lib/pkgconfig/
}
