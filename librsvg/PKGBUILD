# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=librsvg
pkgver=2.40.20
pkgrel=1
arch=('i686' 'x86_64')
license=(LGPL)
url="https://wiki.gnome.org/Projects/LibRsvg"
pkgdesc="SVG rendering library"
depends=(gdk-pixbuf2 pango libcroco)
makedepends=(glib2-devel libxml2-devel gobject-introspection vala gtk-doc)
source=("https://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.40/${pkgname}-${pkgver}.tar.xz"
        "0001-msys-platform-win32.patch")
md5sums=('4949d313b0c5d9161a5c259104af5568'
         '22c1cdf8048d2d1569980d1eb9948767')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 -i ${srcdir}/0001-msys-platform-win32.patch
  autoreconf -fvi
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --enable-gtk-theme \
    --enable-introspection \
    --enable-pixbuf-loader \
    --enable-vala \
    --with-croco \
    --with-svgz
    make
}

package() {
  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
}
