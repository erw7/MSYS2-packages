# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=lcms2
pkgver=2.9
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2"
url="http://www.littlecms.com/"
arch=('i686' 'x86_64')
license=(MIT)
source=("https://downloads.sourceforge.net/sourceforge/lcms/${pkgname}-${pkgver}.tar.gz")
md5sums=('8de1b7724f578d2995c8fdfa35c3ad0e')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
  --prefix=/usr \
  --build=${CHOST}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING \
    -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
