# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=fontconfig
pkgver=2.13.1
pkgrel=1
arch=('i686' 'x86_64')
license=(custom)
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
pkgdesc="A library for configuring and customizing font access"
depends=(libexpat freetype2)
makedepends=(libexpat-devel)
source=("https://www.freedesktop.org/software/fontconfig/release/${pkgname}-${pkgver}.tar.bz2"
        "001-fix-for-DESTDIR-being-ignored.patch")
sha256sums=('f655dd2a986d7aa97e052261b36aa67b0a64989496361eca8d604e6414006741'
            'f44a24c9e22fe23bdd0b6843a1a7177285eda77660903b328269f92f2e76f005')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/001-fix-for-DESTDIR-being-ignored.patch
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
  --prefix=/usr \
  --build=${CHOST} \
  --disable-docs
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
