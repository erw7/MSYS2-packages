# Maintainer: erw7 <erw7.github@gmail.com>

pkgname=('libxext' 'libxext-devel')
pkgver=1.3.4
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
makedepends=('xorg-util-macros' 'libx11-devel')
source=(${url}releases/individual/lib/libXext-${pkgver}.tar.bz2)
sha256sums=('59ad6fcce98deaecc14d39a672cf218ca37aba617c9a0f691cac3bcd28edf82b')

prepare() {
  cd libXext-${pkgver}
  autoreconf -fvi
}

build() {
  cd libXext-${pkgver}
  ./configure \
  --prefix=/usr \
  --build=${CHOST}
  make
  make DESTDIR=${srcdir}/dest install
}

package_libxext() {
  pkgdesc="X11 miscellaneous extensions library"
  depends=('libx11')
  groups=('libraries')

  install -Dm755 ${srcdir}/dest/usr/bin/* -t ${pkgdir}/usr/bin/
  install -Dm644 ${srcdir}/dest/usr/share/doc/libXext/*.xml \
    -t ${pkgdir}/usr/share/doc/libXext/
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/COPYING \
    -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_libxext-devel() {
  pkgdesc="libxext headers and libraries"
  groups=('development')
  depends=("libxext=${pkgver}" 'libx11-devel')

  install -Dm644 ${srcdir}/dest/usr/include/X11/extensions/*.h \
    -t ${pkgdir}/usr/include/X11/extensions/
  install -Dm644 ${srcdir}/dest/usr/lib/libXext*.a \
    -t ${pkgdir}/usr/lib/
  install -Dm644 ${srcdir}/dest/usr/lib/pkgconfig/xext.pc \
    -t ${pkgdir}/usr/lib/pkgconfig/
  install -Dm644 ${srcdir}/dest/usr/share/man/man3/*.3 \
    -t ${pkgdir}/usr/share/man/man3/
}
